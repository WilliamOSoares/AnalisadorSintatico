######## Estrutura dos lexames para os tipos ########

	<NRO> ::= <DIGITO><NROCONT>
	<NROCONT> ::= <> | <DIGITO><NROCONT>

	<REAL> ::= <NRO><REALCONT>
	<REALCONT> ::= <> | .<NRO>

	<DIGITO> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

	<LETRA> ::= a | b | c | d | e | f | g | h | i | j |k |l | m |n | o | p | q | r| s | t | u | v | w | x | y | z 
		| A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z

	<BOOL> ::= verdadeiro | falso

	<IDE> ::= <LETRA> <IDECONT> 
	<IDECONT> ::= <LETRA> <IDECONT> | _ <IDECONT> | <DIGITO><IDECONT> | <>

	<CHARSPEC> ::= \’ | \” 

	<CHAR> ::= ' <LETRA> ' | ' <DIGITO> '| ' <SIMBOLO> ' | ' <CHARSPEC> '

	<CADEIA> ::= " <CADCONT> "
	<CADCONT> ::= <> | <LETRA><CADCONT> | <DIGITO><CADCONT> | <SIMBOLO><CADCONT> | <CHARSPEC><CADCONT>

#### De acordo com a tabela ascii ####
	<SIMBOLO> ::= ' ' | # | $ | , | . | ? | @ | ^ | _ | ` | ~

####### Declaracao de numeros sem o 0 ou 00000 ############

	<NROZ> ::= 0 <NROZCONT> | 1<NROCONT> | 2<NROCONT> | 3<NROCONT> | 4<NROCONT> | 5<NROCONT> | 6<NROCONT> | 7<NROCONT> | 8<NROCONT> | 9<NROCONT>
	<NROZCONT> ::= 0 <NROZCONT>| 1<NROCONT> | 2<NROCONT> | 3<NROCONT> | 4<NROCONT> | 5<NROCONT> | 6<NROCONT> | 7<NROCONT> | 8<NROCONT> | 9<NROCONT>

	<REALZ> ::= <NRO><REALZCONT>
	<REALZCONT> ::= <> | .<NROZ>

######## Construção do código ###########

	<START> ::= algoritmo <ALGORITMO> 
		| funcao <FUNCAO> <START>
		| variaveis <VARIAVEIS> <A>
		| constantes <CONSTANTES> <B>
		| registro <REGISTRO> <START>

	<A> ::= algoritmo <ALGORITMO> 
		| funcao <FUNCAO> <A>
		| constantes <CONSTANTES> <C>
		| registro <REGISTRO> <A>

	<B> ::= algoritmo <ALGORITMO> 
		| funcao <FUNCAO> <B>
		| variaveis <VARIAVEIS> <C>
		| registro <REGISTRO> <B>

	<C> ::= algoritmo <ALGORITMO> 
		| funcao <FUNCAO> <C>
		| registro <REGISTRO> <C>

############ Conteudos dentro das { } dos comandos ################

	<CONTEUDO> ::= variaveis <VARIAVEIS><CONTEUDO> 
		| constantes <CONSTANTES><CONTEUDO> 
		| se ( <SE><CONTEUDO> 
		| enquanto ( <ENQUANTO><CONTEUDO> 
		| leia <LEIA><CONTEUDO>
		| escreva <ESCREVA><CONTEUDO>
		| para <PARA><CONTEUDO>
		| registro <REGISTRO><CONTEUDO> 
		| <ACESSOVAR> = <EXPATRIBUICAOB>;<CONTEUDO>
		| <CHAMADAFUNCAO> ;<CONTEUDO>
		| retorno <RETORNO>
		| }
	
########### COMANDOS #################

	<ALGORITMO> ::= { <CONTEUDO> 

	<FUNCAO> ::= <TIPOA> <TIPOCONT> <IDE> <FUNCAOINIT>
		| vazio <IDE> <FUNCAOINIT>
	<FUNCAOINIT> ::= (<PARANINIT> { <CONTEUDO> }
	<TIPOCONT>::= [] <VETORMAIS>
		| <>
	<VETORMAIS> ::= [] <VETORMAISUM>
		| <>
	<VETORMAISUM> ::= [] 
		| <>

	<PARANINIT> ::= <TIPOA> <IDE> <PARAINITCONT>
	<PARAINITCONT> ::= , <PARANINIT>
		| )

	<RETORNO> ::= <VALOR> ; }

	<CHAMADAFUNCAO> ::= <IDE> (<PARAN>
	<PARAN> ::= <PARANCONT> 
		| )
	<PARANCONT> ::= <VALOR> <PARANFIM> 
	<PARANFIM> ::= , <PARANCONT>
		| )

	<VARIAVEIS> ::= { <VAR> 
	<CONSTANTES> ::= { <CONST> 
		
		<TIPO> ::= inteiro
			| real
			| booleano
			| cadeia
			| char
		<TIPOA> ::= inteiro
			| real
			| booleano
			| cadeia
			| char
			| registro						

		<VAR> ::=  <TIPO> <VARALT>
		<VARALT> ::= <IDE> <VARCONT>
		<VARCONT> ::= <VARINIT> <VARFINAL>
			| <VARFINAL>
		<VARFINAL> ::= , <VARALT>
			| ; <VARFIM>
		<VARFIM> ::= <VAR>
			| }
		<VARINIT> ::= = <VALOR>
			| [<NRO>] <VARINITCONT>
			| [<IDE>] <VARINITCONT>
		<VARINITCONT>::= = { <VETOR> 
			| [<NRO>] <VARINITCONTMATR>
			| [<IDE>] <VARINITCONTMATR>
		<VARINITCONTMATR>::= = { <VETOR> , { <VETOR>
			| [<NRO>] = { <VETOR> , { <VETOR> , { <VETOR>
			| [<IDE>] = { <VETOR> , { <VETOR> , { <VETOR>
		
		<VETOR> ::= <VALOR> <VETORCONT>
		<VETORCONT> ::= , <VETOR>
			| } 

		<NEGATIVO> ::= <REAL>
			| <NRO>
			| <ACESSOVAR>

		<CONST> ::= <TIPO> <CONSTALT>
		<CONSTALT> ::= <IDE> <VARINIT> <CONSTCONT>
	 	<CONSTCONT> ::= , <CONSTALT>
			| ; <CONSTFIM>
		<CONSTFIM> ::= <CONST>
			| }		

	<SE> ::= <EXPRESSAOB>) { <CONTEUDO> } <SENAO> 

	<SENAO> ::= senao { <CONTEUDO> }
		| <>

	<ENQUANTO> ::= <EXPRESSAOB>) { <CONTEUDO> } 

	<LEIA> ::= (<LEIACONT>

	<LEIACONT> ::= <ACESSOVAR> <LEIAFIM>

	<LEIAFIM> ::= , <LEIACONT>
		| );

	<ESCONT> ::= <ACESSOVAR> <ESFIM>
		| <CADEIA> <ESFIM>
		| <CHAR> <ESFIM>

	<ESFIM> ::= , <ESCONT>
		| );

	<ESCREVA> ::= (<ESCONT>
	
	<PARA> ::= (<ACESSOVAR> = <EXPATRIBUICAO>;<PARACONT>
	
	<PARACONT> ::= <EXPRESSAOB>;<PARAFIM>
		| <IDE>; <PARAFIM>
	
	<PARAFIM> ::= <ACESSOVAR> = <EXPRESSAOB>){<CONTEUDO>}
		| <ACESSOVAR> <EXPATRIBUICAOCONTB>){<CONTEUDO>}

	<REGISTRO> ::= <IDE> { <VAR> 

	<ACESSOVAR> ::= . <IDE> [<NRO>] <ACESSOVARCONTB>
		| . <IDE> [<IDE>] <ACESSOVARCONTB>
		| [<NRO>] <ACESSOVARCONTB>	
		| [<IDE>] <ACESSOVARCONTB>	
		| <>
	<ACESSOVARCONT>::= [<NRO>] <ACESSOVARCONTC>
		| [<IDE>] <ACESSOVARCONTC>
		| .<ACESSOVAR>
		| <>
	<ACESSOVARCONTB>::= [<NRO>]
		| [<IDE>]
		| .<ACESSOVAR>
		| <>

############ EXPRESSOES ##############
	<EXPRESSAOB> ::= <EXPARITMETICA> <EXPRESSAOCONTB>
		| <ACESSOVAR> <EXPRESSAOCONTB>
		| <NRO> <EXPRESSAOCONTB>
		| - <NEGATIVO> <EXPRESSAOCONTB>
		| <BOOL> <EXPRESSAOCONTB> 
		| <CHAR> <EXPRESSAOCONTB>
		| <CAD> <EXPRESSAOCONTB>
		| ( <EXPRESSAOB> ) <EXPRESSAOCONTB>
		| ! <EXPREXCB> 
	<EXPREXCB> ::= ( <EXPRESSAOB> ) <EXPRESSAOCONTB>
		| <BOOL> <EXPRESSAOCONTB>
		| <ACESSOVAR> <EXPRESSAOCONTB>

	<EXPRESSAOCONTB> ::= && <EXPRESSAOB>
		| || <EXPRESSAOB>
		| > <EXPRESSAOB>
		| < <EXPRESSAOB>
		| >= <EXPRESSAOB>
		| <= <EXPRESSAOB>
		| != <EXPRESSAOB>
		| == <EXPRESSAOB> 
		| <>

	######### Expressao aritmetica ###########			
			
		<EXPARITMETICA> ::= <ACESSOVAR> <EXPARITMETICACONT>
			| <NRO> <EXPARITMETICACONT>
			| - <NEGATIVO> <EXPARITMETICACONT>
			| ( <EXPARITMETICAPAREN>
		<EXPARITMETICAPAREN> ::= <EXPARITMETICA> ) <EXPARITMETICACONTB> 
			| <NRO>) <EXPARITMETICACONTB>
			| <ACESSOVAR>) <EXPARITMETICACONTB>

		<EXPARITMETICACONT> ::= + <EXPARITMETICAB>
			| - <EXPARITMETICAB>
			| * <EXPARITMETICAB>
			| / <EXPARITMETICAB>

		<EXPARITMETICAB> ::= <ACESSOVAR> <EXPARITMETICACONTB>
			| <NRO> <EXPARITMETICACONTB>
			| - <NEGATIVO> <EXPARITMETICACONTB>
			| ( <EXPARITMETICABPAREN>
		<EXPARITMETICABPAREN> ::= <EXPARITMETICAB> ) <EXPARITMETICACONTB> 
			| <NRO>) <EXPARITMETICACONTB>
			| <ACESSOVAR>) <EXPARITMETICACONTB>

		<EXPARITMETICACONTB> ::= + <EXPARITMETICAB>
			| - <EXPARITMETICAB>
			| * <EXPARITMETICAB>
			| / <EXPARITMETICAB>
			| <>

		<EXPATRIBUICAO> ::= <EXPARITMETICA>
				| <ACESSOVAR> <EXPATRIBUICAOCONT>
				| <NRO> <EXPATRIBUICAOCONT>

		<EXPATRIBUICAOB> ::= <EXPRESSAOB>
				| (<PARAN>

		<EXPATRIBUICAOCONT> ::= ++
				| --
				| <>

		<EXPATRIBUICAOCONTB> ::= ++
				| --
		
		<VALOR> ::= <EXPRESSAOB>
				| <CHAMADAFUNCAO>